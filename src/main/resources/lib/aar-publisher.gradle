apply plugin: 'maven-publish'
apply from: rootProject.buildscript.classLoader.getResource('lib/publishing-repos.gradle').toURI()

publishing {
    publications {
        "release"(MavenPublication) {
            groupId artifactGroupId
            version artifactVersion
            artifactId "${artifactId}"
            artifact("${project.buildDir}/outputs/aar/${libraryName}-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
